% CREATED BY DAVID FRISK, 2016
\AgdaHide{
\begin{code}
open import base
\end{code} }

\chapter{Parsing} \label{Parsing}

	Parsing is the process of analyzing the structure of a sequence of symbols,
	which are often called \emph{tokens}. This may simply consist of
	determining, or \emph{recognizing}, \emph{whether} a sequence conforms to a
	predefined structure, called a \emph{grammar}, or to also create a
	description of \emph{how} the sequence conforms. This deeper analysis is
	called \emph{parsing}, and the resulting description is often a \emph{parse
	tree}.  For our purposes, the context-free grammars describe the
	structures we are interested in.

	The context-free grammars are a class of grammars that can be described by
	a set of production, or rewrite, rules on the form $X \mapsto \alpha$,
	where $X$ is a non-terminal symbol, and $\alpha$ is a list where each
	element may be either a non-terminal or a terminal symbol. Terminal symbols
	are used to represent instances of concrete tokens whereas non-terminal
	symbols are used as labels for more complex structures.
	Non-terminal symbols are often (and always in this thesis) written with
	capital letters, whereas terminals are written in lowercase (but this does
	not mean parsers are only able to handle lowercase inputs).

	At this point, an example is in order: consider a grammar with the rules $Z
	\mapsto Ya$ and $Y \mapsto b$, where $Z$ and $Y$ are non-terminals, and $a$
	and $b$ are terminals.  The sequence $ba$ conforms to the structure of $Z$
	in the grammar, because it can be reduced to $aY$ according to the second
	rule ($Y \mapsto b$), and then reduced to $Z$ according to the first ($Z
	\mapsto Ya$).  Likewise, the rules can be used to produce all sequences
	that conform to the grammar by going the other way: $Z$ can produce $Ya$,
	and the $Y$ in $Ya$ can produce $b$, giving us $ba$.

	Context-free grammars can also be significantly more expressive than the
	simple example given above. Consider the set of rules $\{W \mapsto aWb$, $W
	\mapsto \epsilon\}$. In this grammar, $W$ could be rewritten to either
	$aWb$, or $\epsilon$, the first of which could be rewritten again using the
	rules for $W$, creating either $aaWbb$ or $ab$, the first of which again
	could be rewritten. This means any sequence with any number of $a$s
	followed by an equal number of $b$s conforms to the grammar. Now consider
	another set of rules {$P \mapsto a$, $P \mapsto PP$}. The sequence $aaa$
	conforms to $P$ in this grammar, but it can be rewritten in several ways:
	we could form $PPa$ using rule 1 two times then $Pa$ using rule 2, and then
	form $PP$ then $P$ using rules 1 and 2 again. But we could also have formed
	$aPP$ using rule 1 twice, followed by rules 2, 1, and 2 again to get $P$.
	When this is possible, the grammar is said to be \emph{ambiguous}.

	A grammar is often given a \emph{starting symbol}. This can be considered
	the `topmost' non-terminal symbol in the grammar, and a sequence is said to
	be `in' the grammar if it conforms to the structure of the starting symbol.
	A sequence is also sometimes said to be `in' a list of terminals and 
	non-terminals, meaning it confoms to the structure of said list.

	Here is how we could encode a context-free grammar in Agda:

	\begin{code}
		module Parsing (N T : Set) where

		record CFG : Set where
		  constructor _⟩_
		  field
		    start : N
		    rules : (N × (N ∣ T)* )*
	\end{code}

	Here, type arguments \codett{N} and \codett{T} represent non-terminal and
	terminal symbols, and the record type has two fields: a starting symbol,
	and a set of rules for the grammar.  An example: consider the grammar
	\codett{ S ⟩ (S , r s ∷ l S ∷ ε) ∷ (S , ε) ∷ ε}, where \codett{S} is
	a nonterminal symbol, \codett{s} is a terminal symbol, and \codett{l} and
	\codett{r} are the constructors for the left and right choice of our
	optional type \codett{∣}. This grammak will have conforming strings that
	either are empty, or start with the token \codett{s} followed by any
	sequence that conforms to the \codett{S} non-terminal. The set of
	conforming sequences for this second grammar is the set of (possibly empty)
	sequences only containing the token \emph{'s'}.

	% Quotation is `single' or ``double'' in TeX! Opening quotes are different
	% than closing ones!

	A formal definition of when a list of tokens conforms to a grammar is also
	necessary. Here, we suggest for rules for when a sequence conforms to a
	list of terminal and non-terminal symbols: 

	%% Simpler way to get this math display
	\[
	\frac{\ }{\epsilon\in\epsilon} \qquad
	\frac{u\in A \qquad v\in\alpha}{uv\in A\alpha} \qquad
	\frac{u\in\alpha}{au\in a\alpha} \qquad
	\frac{u\in\alpha}{u\in A}\ A \mapsto \alpha
	\]

	% \begin{table}[h]
	% \centering
	% \begin{tabular}{cccc}
	% 		 \( \displaystyle \frac{\ }{\epsilon\in\epsilon} \) &
	% 		 \( \displaystyle \frac{u\in A\ \ v\in\alpha}{uv\in A\alpha} \) &
	% 		 \( \displaystyle \frac{u\in\alpha}{au\in a\alpha} \) &
	% 		 \( \displaystyle \frac{u\in\alpha}{u\in A}A \mapsto \alpha \)
	% \end{tabular}
	% \end{table}

	The rules state that: Firstly, the empty string conforms to the empty
	structure. Secondly, if a string $u$ conforms to the structure of a
	nonterminal $A$, and $v$ to some sequence $\alpha$, then the concatenation
	of $u$ and $v$ conforms to the sequence $A$ followed by $\alpha$. Thirdly,
	if $u$ conforms to $\alpha$, prepending the terminal symbol $a$ to both is
	also a valid rewrite rule. Finally, if there is a rule $A \mapsto \alpha$,
	any string that conforms to $\alpha$ also conforms to the non-terminal $A$.

	This set of rules can be expressed in Agda using the following data type:

	\begin{code}
		infixr 10 _⊢_∈_
		data _⊢_∈_ (G : CFG) :  T * → (N ∣ T) * → Set where
		  empty :
		    G ⊢ ε ∈ ε

		  concat : {u v : T *} {A : N} {α : (N ∣ T) *} →
		    G ⊢ u ∈ l A ∷ ε → G ⊢ v ∈ α → G ⊢ u ++ v ∈ l A ∷ α

		  term : {u : T *} {a : T} {α : (N ∣ T) *} →
		    G ⊢ u ∈ α → G ⊢ a ∷ u ∈ r a ∷ α

		  nonterm : {A : N} {α : (N ∣ T) *} {u : T *} →
		    (A , α) ∈ CFG.rules G → G ⊢ u ∈ α → G ⊢ u ∈ l A ∷ ε
	\end{code}

	Here, a proposition of the form \codett{G ⊢ u ∈ α} would mean that, given a
	grammar \codett{G}, the sequence of tokens \codett{u} conforms to the
	structure \codett{α}. That is, if \codett{A} is a non-terminal with a
	single rule $A \mapsto aa$ and \codett{a} is a terminal symbol, any value
	with the type \codett{G ⊢ r A ∷ ε ∈ a ∷ a ∷ ε} would constitute a proof
	that the sequence $aa$ conforms to the structure of $A$ (which is just
	\emph{"sequences that are equal to $aa$"}).

	Another possible definition for conformance could be:

	\begin{code}
		infixl 10 _⊢_/_∈_
		data _⊢_/_∈_ (G : CFG) : T * → T * → (N ∣ T)* → Set where
		  empt : {w : T *} →
		    G ⊢ w / w ∈ ε

		  conc : {u v w : T *} {X : N} {α : (N ∣ T) *} →
		    G ⊢ u / v ∈ l X ∷ ε →
		    G ⊢ v / w ∈ α →
		    G ⊢ u / w ∈ l X ∷ α

		  term : {a : T} {u v : T *} {α : (N ∣ T) *} →
		    G ⊢ u / v ∈ α →
		    G ⊢ a ∷ u / v ∈ r a ∷ α

		  nont : {u v : T *} {X : N} {α : (N ∣ T) *} →
		    (X , α) ∈ CFG.rules G →
		    G ⊢ u / v ∈ α →
		    G ⊢ u / v ∈ l X ∷ ε
	\end{code}

	Here, a proposition of the form \codett{G ⊢ v / w ∈ α} would mean that,
	given a grammar \codett{G}, the sequence \codett{v} up to, but not
	including \codett{w} conforms to the structure \codett{α}. This means
	\codett{w} will always be a suffix of \codett{v}. This definition, while
	very similar to the one above, has one advantage: the constructor
	\codett{conc} is not reliant on the list concatenation operator
	\codett{++}, while its counterpart \codett{concat} is. This can simplify
	proofs using this data type, as they will not be dependent on the
	normalization of the \codett{++} operator.

	This second data type can be constructed if the first one can:

	\begin{code}
		sound₀ :  ∀ {G u α} → G ⊢ u ∈ α → G ⊢ u / ε ∈ α

		sound₀ empty = empt
		sound₀ (term a) = term (sound₀ a)
		sound₀ (nonterm x a) = nont x (sound₀ a)
		sound₀ (concat {v = v} a b) = conc (s (sound₀ a)) (sound₀ b)
		  where
		    s : ∀ {u v w α G} →
		      G ⊢ u / v ∈ α →
		      G ⊢ u ++ w / v ++ w ∈ α
	\end{code}
	\AgdaHide{\begin{code}
		    s empt = empt
		    s (conc (conc a empt) a₁) = s (conc a a₁)
		    s (conc (nont x a) a₁) = conc (nont x (s a)) (s a₁)
		    s (term a) = term (s a)
		    s (nont x a) = nont x (s a)
	\end{code}}

	This proof is fairly simple: constructors \codett{empty}, \codett{term},
	and \codett{nonterm} match perfectly with their counterparts \codett{empt},
	\codett{term}, and \codett{nont}. \codett{concat} and \codett{conc} are
	also almost identical, but require that we introduce some string
	concatenation to the first argument. It is also possible to go the other
	way, and construct the first data type from the second:

	\begin{code}
		complete₀ : ∀ {u v α G} → G ⊢ u / v ∈ α →
		  Σ λ t → (G ⊢ t ∈ α) × (t ++ v ≡ u)

		complete₀ empt = σ ε (empty , refl)
		complete₀ (term g) with complete₀ g
		complete₀ (term g) | σ p₁ (x₀ , x₁) = σ (_ ∷ p₁) (term x₀ , (app (_ ∷_) x₁))
		complete₀ (nont x g) with complete₀ g
		complete₀ (nont x g) | σ p₁ (x₀ , x₁) = σ p₁ (nonterm x x₀ , x₁)
		complete₀ (conc g g₁) with complete₀ g , complete₀ g₁
		complete₀ (conc g g₁) | σ p₁ (x₀ , x₁) , σ q₁ (y₀ , y₁) =
		  σ (p₁ ++ q₁) ((concat x₀ y₀) , p₁++q₁++v≡u)
	\end{code}
	\AgdaHide{
	\begin{code}
		  where
		    p₁++q₁++v≡u =
		      trans (trans (assoc-++ p₁ q₁ _) (sym (app (p₁ ++_) y₁))) (sym x₁)
	\end{code}}

	The \codett{Σ λ \_ → \_} notation used here is a dependent pair, in this
	case of a sequence $t$ and a proof that it is both in $\alpha$ and that it
	is a prefix of $v$ ending before $u$. We use this type because there is no
	explicit reference to the parsed sequence in the \codett{\_⊢\_/\_∈\_} type,
	but it is required for \codett{\_⊢\_∈\_}. Apart from the
	\codett{with}-statements used to match on the recursive calls used for 
	constructing $t$, the structure of \codett{complete₀} is similar to that of
	\codett{sound₀}. The equality proof \codett{p₁++q₁++v≡u} is not shown here.

	One problem with these data types is that it is possible to construct
	several different proofs for the same derivation of the same rule using the
	conc and empt rules:

	\begin{code}
		same-but-different : ∀ {u v X G} →
		  G ⊢ u / v ∈ l X ∷ ε →
		  G ⊢ u / v ∈ l X ∷ ε
		same-but-different g = conc g empt
	\end{code}

	This rule could be applied to the rule any number of times, and as such two
	proofs may not be equal even if they represent the same derivation of the
	same rule. This will make it harder for us to reason about ambiguous
	derivations, where the same structure can be derived from a sequence in
	many different distinct ways. This problem is not present in the following
	data structure.

	\begin{code}
		infixl 10 _⊢_∥_∈_
		data _⊢_∥_∈_ (G : CFG) : T * → T * → (N ∣ T)* → Set where
		  empt : {w : T *} →
		    G ⊢ w ∥ w ∈ ε

		  conc : {u v w : T *} {X : N} {α β : (N ∣ T) *} →
		    (X , α) ∈ CFG.rules G →
		    G ⊢ u ∥ v ∈ α →
		    G ⊢ v ∥ w ∈ β →
		      G ⊢ u ∥ w ∈ l X ∷ β

		  term : {a : T} {u v : T *} {α : (N ∣ T) *} →
		    G ⊢ u ∥ v ∈ α →
		      G ⊢ a ∷ u ∥ v ∈ r a ∷ α
	\end{code}
	
	This data structure merges the concatenation and non-terminal constructors,
	so that concatenation can only be performed when introducing a new
	non-terminal. This ensures that each constructor makes progress and
	therefore prevents the problem above. This data structure is both sound and
	complete with respect to the previous definitions, and the proofs for this
	generally follows the same structure as the proofs between the previous two
	types, and will therefore not be discussed in detail here.

	Of course, we could have started with this definition for membership in a
	grammar, without relating it the earlier definitions. However, as these
	data types form the basis on which we construct the correctness proofs of
	our implementation, it is important that they are sensible. Since it is
	also not possible to prove the correctness of definitions themselves, other
	than showing their equivalence to other models, we felt it worthwhile to
	discuss several variants of this definition.
